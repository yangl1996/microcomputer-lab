DATA	SEGMENT
LED		DB 3FH, 06H, 5BH, 4FH, 66H, 6DH, 7DH, 07H
		DB 7FH, 67H, 77H, 7CH, 39H, 5EH, 79H, 71H
TIME	DB 00H
CNT		DB 00H
DATA	ENDS

CODE 	SEGMENT
	ASSUME CS:CODE, DS:DATA
START:	MOV AX, DATA
		MOV DS, AX			; INIT DATA SEGMENT
		MOV DX, 0E48BH
		MOV AL, 10000000B
		OUT DX, AL			; SET A AND C BOTH OUT
INOUT:	MOV BX, OFFSET LED	; SET TABLE BASE ADDRESS
FLASH:	MOV AL, 01H
		MOV DX, 0E488H
		OUT DX, AL			; ENABLE LOWER DIGIT
		MOV AL, TIME
		AND AL, 0FH			; PRESERVE LOWER 4 BITS IN AL
		XLAT				; TABLE LOOKUP
		MOV DX, 0E48AH
		OUT DX, AL			; PRINT LOW DIGIT
		MOV AH, 86H
		MOV CX, 0
		MOV DX, 2710H		; INTERVAL: 10000 MICROSEC
		INT 15H				; WAIT
		MOV AL, 02H
		MOV DX, 0E488H
		OUT DX, AL			; ENABLE HIGHER DIGIT
		MOV AL, TIME
		MOV CL, 4
		SHR AL, CL			; PRESERVE HIGHER 4 BITS IN AL
		XLAT
		MOV DX, 0E48AH
		OUT DX, AL			; PRINT HIGH DIGIT
		MOV AH, 86H
		MOV CX, 0
		MOV DX, 2710H
		INT 15H				; WAIT
		INC CNT
		CMP CNT, 50
		JG INCT				; IF LOOP 25 TIMES THEN INC TIME
		JMP FLASH
INCT:	MOV CNT, 0
		MOV AL, TIME
		ADD AL, 1
		DAA					; INCREASE TIME
		MOV TIME, AL
		MOV AH, 1
		INT 16H
		JZ INOUT
		MOV AH, 4CH
		INT 21H
CODE ENDS
END START

