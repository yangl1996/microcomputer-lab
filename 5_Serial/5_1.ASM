STACK SEGMENT PARA STACK
	DW 128H DUP(0)
STACK ENDS

DATA SEGMENT

DATA ENDS

CODE SEGMENT
ASSUME CS:CODE, SS:STACK, DS:DATA
START:
	; SET TIMER
	MOV DX, 0E483H
	MOV AL, 00010110B
	OUT DX, AL
	MOV DX, 0E480H
	MOV AL, 52
	OUT DX, AL
	; SET DATA SEGMENT
	MOV AX, DATA
	MOV DS, AX
	; 8251A INIT
	MOV DX, 0E4B9H
	MOV AL, 0
	OUT DX, AL
	OUT DX, AL
	OUT DX, AL			; SAFE INIT
	MOV AL, 40H
	OUT DX, AL			; INTERNAL RESET
	MOV AL, 4EH
	OUT DX, AL			; ASYNC, 1 STOP, 8 DATA, NO PARITY, 16 FACTOR
	MOV AL, 27H
	OUT DX, AL			; ENABLE TX/RX

TXRX:
	; READ IN KEYBOARD
	MOV AH, 01H
	INT 21H
	CMP AL, 27			; CHECK WHETHER IS ESCAPE
	MOV BL, AL
	JE GOODBYE

	; TX
WAITTX:
	MOV DX, 0E4B9H
	IN AL, DX
	AND AL, 1			; PRESERVE TXRDY BIT
	JZ WAITTX			; IF NOT TXRDY, WAIT ANOTHER LOOP

	; ACTUALLY TRANSMIT DATA
	MOV DX, 0E4B8H
	MOV AL, BL
	OUT DX, AL
	
	; RX
WAITRX:
	MOV DX, 0E4B9H
	IN AL, DX
	AND AL, 010B		; PRESERVE RXRDY BIT
	JZ WAITRX			; IF NOT READY, WAIT ANOTHER LOOP

	; PRINT DATA ON THE SCREEN
	MOV DX, 0E4B8H
	IN AL, DX			; READ FROM SERIAL PORT
	MOV DL, AL
	MOV AH, 02H
	INT 21H

	JMP TXRX
	
GOODBYE:
	MOV AH, 4CH
	INT 21H

CODE ENDS
END START
